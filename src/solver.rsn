
fn solve(grid: Grid) -> arr Grid {
    val unsatCell = !inferenceSolve(grid);
    if unsatCell {
        return arr Grid[0];
    } else if isComplete(grid){
        return [grid]
    } else {
        val guessCellPos = cellWithLeastCandidates(grid);
        assert(coordsNonNeg(guessCellPos), "negative coordinate");
        val guessCell = cellAt(grid, guessCellPos);
        val packedCandidates = packedCandidates(guessCell.candidates);
        val solsPerGuess = arr arr Grid[#packedCandidates];
        for var i = 0; i < #packedCandidates; i += 1 {
            val copy = copyGrid(grid);
            cellAt(copy, guessCellPos).content = packedCandidates[i];
            solsPerGuess[i] = solve(copy);
        };
        return flattenSolsArray(solsPerGuess);
    }
}

const r_UNSAT = -10;
const r_UNCHANGED = -9;
const r_UPDATED = -8;

// Fills the cells that can be inferred without guessing
// Returns false if at least 1 cell has no candidate left,
// or at least 1 region has no possible position for some number,
// true otherwise
fn inferenceSolve(grid: Grid) -> Bool {
    while true {
        nullifyImpossibleCandidates(grid);
        val uniquePosInferRet = inferOneNumberWithUniquePos(grid);
        if uniquePosInferRet == r_UNSAT {
            return false
        } else if uniquePosInferRet == r_UPDATED {
            nullifyImpossibleCandidates(grid);
        };
        val uniqueInferRet = inferOneUniqueCandidate(grid);
        if uniqueInferRet == r_UNSAT {
            return false
        } else if uniqueInferRet == r_UNCHANGED {
            return true
        };
    };
    panic "cannot happen"
}

fn nullifyImpossibleCandidates(grid: Grid) -> Bool {
    var modified = false;
    for var r = 0; r < grid.size; r += 1 {
        for var c = 0; c < grid.size; c += 1 {
            if grid.cells[r][c].content == c_EMPTY_CELL {
                modified = nullifyCandidatesInRow(grid, r, c) || modified;
                modified = nullifyCandidatesInCol(grid, r, c) || modified;
                modified = nullifyCandidatesInSector(grid, r, c) || modified;
            }
        }
    };
    return modified;
}

// Iterates the cells until one of the following happens:
//   - A cell is found that has no candidate left -> return r_UNSAT
//   - A cell is found that has exactly one candidate left -> assign this candidate as the cell value and return r_UPDATED
//   - All the cells have been considered -> return r_UNCHANGED
fn inferOneUniqueCandidate(grid: Grid) -> Int {
    for var r = 0; r < grid.size; r += 1 {
        for var c = 0; c < grid.size; c += 1 {
            val cell = grid.cells[r][c];
            if cell.content == c_EMPTY_CELL {
                val uc = uniqueCandidate(cell.candidates);
                if uc == -1 {
                    return r_UNSAT;
                } else if uc > 0 {
                    cell.content = uc;
                    return r_UPDATED;
                }
            }
        }
    };
    return r_UNCHANGED
}

// For each row, column and sector, look for a number which can only be set at one position, and set it
// Exit as soon as one such number is found and set
// Return code:
//   r_UNSAT: there is a number in some sector such that no position can accept it
//   r_UNCHANGED: no number was set
//   r_UPDATED: one such number has been found and set
fn inferOneNumberWithUniquePos(grid: Grid) -> Int {
    for var i = 0; i < grid.size; i += 1 {
        for var candidate = 1; candidate <= grid.size; candidate += 1 {
            val ret = placeInRow(grid, i, candidate);
            if ret == r_UNSAT || ret == r_UPDATED {
                return ret
            }
        }
    };
    for var i = 0; i < grid.size; i += 1 {
        for var candidate = 1; candidate <= grid.size; candidate += 1 {
            val ret = placeInCol(grid, i, candidate);
            if ret == r_UNSAT || ret == r_UPDATED {
                return ret
            }
        }
    };
    val sectorSize = sqrt(grid.size);
    for var sr = 0; sr < sectorSize; sr += 1 {
        for var sc = 0; sc < sectorSize; sc += 1 {
            for var candidate = 1; candidate <= grid.size; candidate += 1 {
                val ret = placeInSector(grid, sr, sc, candidate);
                if ret == r_UNSAT || ret == r_UPDATED {
                    return ret
                }
            }
        }
    };
    return r_UNCHANGED;
}

// Iterates the assigned row until one of the following happens:
//  - It finds a cell which already contains numberToPlace -> return r_UNCHANGED
//  - It finds more than one cell which have numberToPlace as a candidate -> return r_UNCHANGED
//  - It has iterated the whole row and not found any cell which has numberToPlace as a candidate -> return r_UNSAT
//  - It has iterated the whole row and found exactly one cell which has numberToPlace as a candidate
//          -> assign numberToPlace to this cell and return r_UPDATED
fn placeInRow(grid: Grid, rowIdx: Int, numberToPlace: Int) -> Int {
    val row = grid.cells[rowIdx];
    val c_NO_POS_IDX = -1;
    var possIdx = c_NO_POS_IDX;
    for var i = 0; i < grid.size; i += 1 {
        val currCell = row[i];
        if currCell.content == c_EMPTY_CELL && hasCandidate(currCell, numberToPlace){
            if possIdx == c_NO_POS_IDX {
                possIdx = i;
            } else {
                return r_UNCHANGED;
            }
        } else if currCell.content == numberToPlace {
            return r_UNCHANGED;
        }
    };
    if possIdx == c_NO_POS_IDX {
        return r_UNSAT;
    } else {
        row[possIdx].content = numberToPlace;
        return r_UPDATED;
    }
}

// Analogous to placeInRow
fn placeInCol(grid: Grid, colIdx: Int, numberToPlace: Int) -> Int {
    val c_NO_POS_IDX = -1;
    var possIdx = c_NO_POS_IDX;
    for var i = 0; i < grid.size; i += 1 {
        val currCell = grid.cells[i][colIdx];
        if currCell.content == c_EMPTY_CELL && hasCandidate(currCell, numberToPlace){
            if possIdx == c_NO_POS_IDX {
                possIdx = i;
            } else {
                return r_UNCHANGED;
            }
        } else if currCell.content == numberToPlace {
            return r_UNCHANGED;
        }
    };
    if possIdx == c_NO_POS_IDX {
        return r_UNSAT;
    } else {
        grid.cells[possIdx][colIdx].content = numberToPlace;
        return r_UPDATED;
    }
}

// Analogous to placeInRow
fn placeInSector(grid: Grid, sectorRowIdx: Int, sectorColIdx: Int, numberToPlace: Int) -> Int {
    val sectorSize = sqrt(grid.size);
    require(0 <= sectorRowIdx && sectorRowIdx < sectorSize, "sector row out of bounds");
    require(0 <= sectorColIdx && sectorColIdx < sectorSize, "sector column out of bounds");
    val c_NO_POS_IDX = -1;
    var possRow = c_NO_POS_IDX;
    var possCol = c_NO_POS_IDX;
    val minSectorRow = sectorRowIdx * sectorSize;
    val minSectorCol = sectorColIdx * sectorSize;
    for var r = minSectorRow; r < minSectorRow + sectorSize; r += 1 {
        for var c = minSectorCol; c < minSectorCol + sectorSize; c += 1 {
            val currCell = grid.cells[r][c];
            if currCell.content == c_EMPTY_CELL && hasCandidate(currCell, numberToPlace){
                if possRow == c_NO_POS_IDX {
                    possRow = r;
                    possCol = c;
                } else {
                    return r_UNCHANGED;
                }
            } else if currCell.content == numberToPlace {
                return r_UNCHANGED;
            }
        }
    };
    if possRow == c_NO_POS_IDX {
        return r_UNSAT;
    } else {
        grid.cells[possRow][possCol].content = numberToPlace;
        return r_UPDATED;
    }
}

fn hasCandidate(cell: Cell, candidate: Int) -> Bool {
    for var i = 0; i < #cell.candidates; i += 1 {
        if cell.candidates[i] == candidate {
            return true
        }
    };
    return false
}

fn nullifyCandidatesInRow(grid: Grid, row: Int, col: Int) -> Bool {
    val cell = grid.cells[row][col];
    var modified = false;
    for var i = 0; i < grid.size; i += 1 {
        if i != col {
            val candidateToNullify = grid.cells[row][i].content;
            if candidateToNullify != c_EMPTY_CELL {
                modified = nullifyCandidate(cell.candidates, candidateToNullify) || modified;
            }
        }
    };
    return modified
}

fn nullifyCandidatesInCol(grid: Grid, row: Int, col: Int) -> Bool {
    val cell = grid.cells[row][col];
    var modified = false;
    for var i = 0; i < grid.size; i += 1 {
        if i != row {
            val candidateToNullify = grid.cells[i][col].content;
            if candidateToNullify != c_EMPTY_CELL {
                modified = nullifyCandidate(cell.candidates, candidateToNullify) || modified;
            }
        }
    };
    return modified
}

fn nullifyCandidatesInSector(grid: Grid, row: Int, col: Int) -> Bool {
    val cell = grid.cells[row][col];
    val sectorSize = sqrt(grid.size);
    val minRow = row / sectorSize * sectorSize;
    val minCol = col / sectorSize * sectorSize;
    var modified = false;
    for var r = minRow; r < minRow + sectorSize; r += 1 {
        for var c = minCol; c < minCol + sectorSize; c += 1 {
            if !(r == row && c == col){
                val candidateToNullify = grid.cells[r][c].content;
                if candidateToNullify != c_EMPTY_CELL {
                    modified = nullifyCandidate(cell.candidates, candidateToNullify) || modified;
                }
            }
        }
    };
    return modified
}

// Replaces all occurrences of candidateToNullify in candidates with 0
fn nullifyCandidate(candidates: mut arr Int, candidateToNullify: Int) -> Bool {
    require(candidateToNullify > 0, "candidate to nullify must be positive, was " + intToString(candidateToNullify));
    var modified = false;
    for var i = 0; i < #candidates; i += 1 {
        if candidates[i] == candidateToNullify {
            candidates[i] = 0;
            modified = true;
        }
    };
    return modified
}

// Returns the unique candidate if there is exactly 1 candidate
// If there are more than 1 candidate, returns 0
// If there is no candidate, returns -1
fn uniqueCandidate(candidates: arr Int) -> Int {
    var unique = -1;
    for var i = 0; i < #candidates; i += 1 {
        if candidates[i] != 0 {
            if unique == -1 {
                unique = candidates[i];
            } else {
                return 0;
            }
        }
    };
    return unique;
}

fn cellWithLeastCandidates(grid: Grid) -> CellPos {
    var bestCellPos = new CellPos { -1, -1 };
    var bestNumCandidates = grid.size;
    for var r = 0; r < grid.size; r += 1 {
        for var c = 0; c < grid.size; c += 1 {
            val currCell = grid.cells[r][c];
            if currCell.content == c_EMPTY_CELL {
                val currNumCandidates = numCandidates(currCell.candidates);
                if currNumCandidates < bestNumCandidates {
                    bestCellPos = new CellPos { r, c };
                    bestNumCandidates = currNumCandidates
                }
            };
        }
    };
    return bestCellPos
}

fn numCandidates(candidates: arr Int) -> Int {
    var cnt = 0;
    for var i = 0; i < #candidates; i += 1 {
        if candidates[i] > 0 {
            cnt += 1;
        }
    };
    return cnt
}

fn packedCandidates(candidates: arr Int) -> arr Int {
    val nCandidates = numCandidates(candidates);
    val packed = arr Int[nCandidates];
    for var rci = 0, var pci = 0; rci < #candidates; rci += 1 {
        if candidates[rci] > 0 {
            packed[pci] = candidates[rci];
            pci += 1;
        }
    };
    return packed
}

fn flattenSolsArray(sols: arr arr Grid) -> arr Grid {
    var nSols = 0;
    for var i = 0; i < #sols; i += 1 {
        nSols += #sols[i];
    };
    val flat = arr Grid[nSols];
    var flatIdx = 0;
    for var i = 0; i < #sols; i += 1 {
        for var j = 0; j < #sols[i]; j += 1 {
            flat[flatIdx] = sols[i][j];
            flatIdx += 1;
        }
    };
    return flat;
}
